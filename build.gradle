// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'versions.gradle'

    addRepos(repositories)

    ext.kotlin_version = '1.5.21'

    dependencies {
        classpath 'com.android.tools:r8:2.2.64'
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.16.0'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.4.20"
    }
}

allprojects {
    addRepos(repositories)

    //Support @JvmDefault
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs = ['-Xjvm-default=all', '-Xopt-in=kotlin.RequiresOptIn']
            jvmTarget = '1.8'
            includeRuntime = true
        }
    }

    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            sonatypeHost = null
        }
    }
}

subprojects {
    afterEvaluate {
        android {
            compileSdkVersion 30
            buildToolsVersion '30.0.2'

            defaultConfig {
                minSdkVersion 19
                targetSdkVersion 30
                versionCode 1
                versionName "$VERSION_NAME"
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                vectorDrawables.useSupportLibrary = true
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }
        // global dependencies for all modules
        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
            testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}